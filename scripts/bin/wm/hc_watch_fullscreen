#!/usr/bin/env -S bb -cp ${HOME}/bin
;; TODO auto-detect bb.edn (https://github.com/babashka/babashka#869 ?)
;; or is this just misconfiguration on my part?

(ns wm.hc-watch-fullscreen
  "Herbstluftwm script to hide polybar when showing a fullscreen window."
  (:require
   [babashka.fs :as fs]
   [babashka.process :refer [sh]]
   [bb.util :refer [sh-out cmd-reader]]
   [clojure.string :refer [split trim]]))

(defn polybar-disabled?
  "Return whether polybar is completely disabled"
  []
  (fs/exists? (format "/tmp/xtoggle/polybar_%s_off" (System/getenv "DISPLAY"))))

(defn on-monitor-0?
  "Return whether monitor 0 is currently focused."
  []
  (= (sh-out "herbstclient attr monitors.focus.index")
     "0"))

(defn is-fullscreen?
  []
  ;; if fullscreen will be false (or will fail/no output on empty tag)
  (= (sh-out "herbstclient attr clients.focus.fullscreen")
     "true"))

(defn toggle-polybar-state
  "Hide or show polybar.
  If no arguments are given, hide polybar in fullscreen or show it otherwise."
  ([hide]
   (if hide
     (sh "xdotool search --class polybar windowunmap %@")
     (sh "xdotool search --class polybar windowmap %@")))
  ([]
   (toggle-polybar-state (is-fullscreen?))))

(defn -main
  "Hide/show polybar based on changes to clients.focus.fullscreen events."
  []
  (sh "herbstclient watch clients.focus.fullscreen")
  (sh "herbstclient watch monitors.focus.index")
  (with-open [rdr (cmd-reader
                   ["herbstclient"
                    "--idle"
                    "attribute_changed"
                    "clients.focus.fullscreen|monitors.focus.index"])]
    (binding [*in* rdr]
      (loop [[_ attr old new] (split (read-line) #"\t")]
        (when-not (polybar-disabled?)
          (cond (= attr "monitors.focus.index")
                (when (= new 0)
                  (toggle-polybar-state))
                :else
                (when (on-monitor-0?)
                  ;; hide if new state is fullscreen; otherwise false or nil (no
                  ;; client)
                  (toggle-polybar-state (= new "true")))))
        (recur (split (read-line) #"\t"))))))

(when (= *file* (System/getProperty "babashka.file"))
  (-main))
